INCLUDE="deck-common.nde"
INCLUDE="deck-macros.nde"

;info
[suits]=spades.png|hearts.png|diamonds.png|clubs.png
[colors]=#000000|#FF0000|#FF0000|#000000
[deckname]=extended.png

[numsuits]=4
[numcards]=69
[numlines]=83
[cardrange]="1-69"
[cardspersuit]={numcards\numsuits}

;rectangle=[cardrange],[wid_border],[ht_border_mod],[wid_internal],[ht_internal_mod],#DDDDDD

LINK = "extended.csv"
CARDS=[numcards]
[img_suit]=JOIN([dir_img],"/",[suits])

FOR=a,1,[numlines]
  FOR=b,0,[numsuits]-1
    ; make Ace of Spade bigger. swap [base_scale] with [ace_scale]
     if = ({suits?{b+1}} = spades.png) _AND_ ({cornervalue?a} = A)
      IMAGE = {{cardvalue?a}+17*b}, {img_suit?{b+1}}, {[wid_internal]*({x?a}/[wid_cells]) - ({scale?a}*[ace_scale])/2 + [wid_border]}, {[ht_internal] *({y?a}/[ht_cells])  - {{scale?a}*[ace_scale]}/2 + [ht_border]}, {{scale?a}*[ace_scale]}, {{scale?a}*[ace_scale]},{angle?a},{flags?a}
    else
      IMAGE= {{cardvalue?a}+17*b}, {img_suit?{b+1}}, {[wid_internal]*({x?a}/[wid_cells]) - ({scale?a}*[base_scale])/2 + [wid_border]}, {[ht_internal] *({y?a}/[ht_cells])  - {{scale?a}*[base_scale]}/2 + [ht_border]}, {{scale?a}*[base_scale]}, {{scale?a}*[base_scale]},{angle?a},{flags?a}
    endif
    if = {cornervalue?a} > 10
        CORNER_ICON2 = {{cardvalue?a}+17*b},{img_suit?{b+1}},{cornervalue?a},{colors?{b+1}}
    else
        CORNER_ICON = {{cardvalue?a}+17*b},{img_suit?{b+1}},{cornervalue?a},{colors?{b+1}}
    endif
    if = {imgface?a} <> 0
       image={{cardvalue?a}+17*b},{imgface?a},50%-[wid_face]/2,{yface?a},[wid_face],[ht_face],0,TP
    endif
  NEXT
NEXT
SAVETTS=[numcards],[dir_tts]/[deckname]
